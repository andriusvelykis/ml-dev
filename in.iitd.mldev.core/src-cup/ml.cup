/** 
 * Standard ML grammar
 */

package in.iitd.mldev.core.parse;

import in.iitd.mldev.core.parse.ast.*;
import java.util.List;
import java.util.ArrayList;

action code {:
  /** Returns an empty list. */
  private <T> List<T> list () {return new ArrayList<T>();}
  
  /** Returns a list containing only the given object. */
  private <T, S extends T> List<T> list (S o) {List<T> l = list(); append(l,o); return l;}
  
  /** Appends the given object to the end of the list. */
  private <T> List<T> append (List<T> l, T o) {l.add(o); return l;}
  
  /** Prepends the given object to the beginning of the list. */
  private <T> List<T> prepend (T o, List<T> l) {l.add(0,o); return l;}
  
  /** Concatenates two lists. */
  private <T> List<T> concat (List<T> l1, List<T> l2) {l1.addAll(l2); return l1;}
  
  private static <T extends AST> T withPos(T obj, int l, int r) {
    obj.setPos(l, r);
	return obj;
  }
  
:}

parser code {:
  /** List of parse errors. */
  public List<java_cup.runtime.Symbol> parseErrors;
  
  /** This method is called when a syntax error has been detected
   * and recovery is about to be invoked. This parser adds
   * the offending token to the parseErrors list. */
  public void syntax_error (java_cup.runtime.Symbol token) {
    parseErrors.add(token);
  }
:}

init with {:
    parseErrors = new ArrayList<java_cup.runtime.Symbol>();
:}

// Terminals

	terminal String ID, TYVAR;
	terminal Integer INT;
	terminal Float REAL;
	terminal String STRING, CHAR;

	terminal ABSTYPE, AND, ANDALSO, AS, CASE, DATATYPE, DO,
		ELSE, END, EQTYPE, EXCEPTION, FN, FUN, FUNCTOR, FUNSIG, HANDLE,
		IF, IN, INCLUDE, INFIX, INFIXR, LAZY, LET, LOCAL, NONFIX,
		OF, OP, OPEN, ORELSE, RAISE, REC, SHARING, SIG, SIGNATURE,
		STRUCT, STRUCTURE, THEN, TYPE, VAL, WHERE, WHILE, WITH, WITHTYPE;

	terminal LPAREN, RPAREN, LBRACKET, LVECTOR, RBRACKET, LBRACE, RBRACE,
		DOT, COMMA, COLON, COLONGT, SEMIC, DOTDOTDOT, UNDER, BAR, EQUALS,
		DARROW, ARROW, HASH, ASTERISK;
	
// Non-terminals

	nonterminal ASTRoot sml;

	nonterminal Ident ident, ident1, ident2, tyvar, tycon,
		qident, qident1, qident2, longident2;
	nonterminal Exp exp, atexp;
	nonterminal Label label;
	nonterminal Match match;
	nonterminal Dec dec, topdec;
	nonterminal List<ValBind> valbind;
	nonterminal List<RecValBind> recvalbind;
	nonterminal List<FunBind> funbind;
	nonterminal List<ConBind> conbind;
	nonterminal List<ExnBind> exnbind;
	nonterminal List<TypeBind> typebind;
	nonterminal List<DatatypeBind> datatypebind;
	nonterminal List<Clause> clauses;
	nonterminal Pat pat, atpat;
	nonterminal ExpField expfield;
	nonterminal Field patfield;
	nonterminal TypeField typefield;
	nonterminal Type type, attype;
	nonterminal Fixity fixity;
	
	nonterminal Str str, fctarg;
	nonterminal Sig sig;
	nonterminal Dec strdec, strdec_;
	nonterminal List<StrBind> strbind;
	nonterminal List<FctBind> fctbind;
	nonterminal List<SigBind> sigbind;
	nonterminal List<FunsigBind> funsigbind;
	nonterminal Spec spec;
	
	nonterminal op_;
	nonterminal Type constraint;

	nonterminal String id1, id2;
	nonterminal List<Ident> qidents1, idents2,
		patheqn, tyvars, tyvarseq;
	nonterminal List<String> qualifier;
	nonterminal List<ExpField> exprecord;
	nonterminal List<Field> patrecord;
	nonterminal List<TypeField> typerecord;
	nonterminal List<Exp> expseq, exps, appexp;
	nonterminal List<Rule> rules;
	nonterminal List<Dec> decs, strdecs, strdecs_, topdecs;
	nonterminal List<Pat> pats, atpats, orpat;
	nonterminal List<Type> typetuple, types;
	nonterminal List<Str> fctargs;
	nonterminal List<Whspec> wherespecs;
	nonterminal List<Spec> specs;
	nonterminal List<StrDesc> strdesc;
	nonterminal List<FctDesc> fctdesc;
	nonterminal List<TypeDesc> typedesc;
	nonterminal List<ValDesc> valdesc;
	nonterminal List<ExnDesc> exndesc;
	nonterminal List<ParamDesc> paramdescs;
	nonterminal List<SharingDesc> sharingdesc;
	nonterminal ParamDesc paramdesc;

// Precedences

	precedence nonassoc WITHTYPE;
	precedence left AND;
	precedence right ARROW;
	precedence right DARROW;
	precedence right BAR;
	precedence left DO;
	precedence left ELSE;
	precedence left RAISE;
	precedence right HANDLE;
	precedence left ORELSE;
	precedence left ANDALSO;
	precedence right AS;
	precedence right COLON;

// Grammar

	sml ::= topdecs:ds {: RESULT = withPos(new ASTRoot(ds),dsleft,dsright); :};

	// Basic rules
	
	id1 ::=
		ID:id {: RESULT = id; :} |
		ASTERISK {: RESULT = "*"; :};
	id2 ::=
		ID:id {: RESULT = id; :} |
		ASTERISK {: RESULT = "*"; :} |
		EQUALS {: RESULT = "="; :};

	ident ::= ID:id {: RESULT = withPos(new Ident(id),idleft,idright); :};
	ident1 ::= id1:id {: RESULT = withPos(new Ident(id),idleft,idright); :};
	ident2 ::= id2:id {: RESULT = withPos(new Ident(id),idleft,idright); :};

	tyvar ::= TYVAR:tv {: RESULT = withPos(new Ident(tv),tvleft,tvright); :};

	qualifier ::=
			{: RESULT = list(); :} |
		qualifier:q ID:id DOT {: RESULT = prepend(id,q); :};
	
	qident ::= qualifier:q ID:id {: RESULT = withPos(new Ident(q,id),qleft,idright); :};
	qident1 ::= qualifier:q id1:id {: RESULT = withPos(new Ident(q,id),qleft,idright); :};
	qident2 ::= qualifier:q id2:id {: RESULT = withPos(new Ident(q,id),qleft,idright); :};
	longident2 ::= ID:id1 DOT qualifier:q id2:id2 {: RESULT = withPos(new Ident(append(q,id1),id2),id1left,id2right); :};

	qidents1 ::=
		qident1:id {: RESULT = list(id); :} |
		qidents1:ids qident1:id {: RESULT = append(ids,id); :};

	idents2 ::=
		ident2:id {: RESULT = list(id); :} |
		idents2:ids ident2:id {: RESULT = append(ids,id); :};
	
	tyvars ::=
			{: RESULT = list(); :} |
		tyvar:tv {: RESULT = list(tv); :} |
		LPAREN tyvarseq:tvs RPAREN {: RESULT = tvs; :};
	
	tyvarseq ::=
		tyvar:tv {: RESULT = list(tv); :} |
		tyvarseq:tvs COMMA tyvar:tv {: RESULT = append(tvs,tv); :};

	constraint ::=
		{: RESULT = null; :} |
		COLON type:t {: RESULT = t; :};
	
	op_ ::= | OP;

	// Expressions

	atexp ::=
		ident2:id
			{: RESULT = withPos(new VarExp(id),idleft,idright); :} |
		OP:o ident2:id
			{: RESULT = withPos(new VarExp(id, true),oleft,idright); :} |
		longident2:id
			{: RESULT = withPos(new VarExp(id),idleft,idright); :} |
		INT:i
			{: RESULT = withPos(new IntExp(i),ileft,iright); :} |
		REAL:r
			{: RESULT = withPos(new RealExp(r),rleft,rright); :} |
		STRING:s
			{: RESULT = withPos(new StringExp(s),sleft,sright); :} |
		CHAR:c
			{: RESULT = withPos(new CharExp(c),cleft,cright); :} |
		HASH:h label:l
			{: RESULT = withPos(new SelectorExp(l),hleft,lright); :} |
		LBRACE:l exprecord:er RBRACE:r
			{: RESULT = withPos(new RecordExp(er),lleft,rright); :} |
		LBRACE:l RBRACE:r
			{: RESULT = withPos(new RecordExp(),lleft,rright); :} |
		LPAREN:l RPAREN:r
			{: RESULT = withPos(new UnitExp(),lleft,rright); :} |
		LPAREN:l exp:e RPAREN:r
			{: RESULT = withPos(e,lleft,rright); :} |
		LPAREN:l expseq:es RPAREN:r
			{: RESULT = withPos(new SeqExp(es),lleft,rright); :} |
		LPAREN:l exp:e COMMA exps:es RPAREN:r
			{: RESULT = withPos(new TupleExp(prepend(e,es)),lleft,rright); :} |
		LBRACKET:l exps:es RBRACKET:r
			{: RESULT = withPos(new ListExp(es),lleft,rright); :} |
		LBRACKET:l RBRACKET:r
			{: RESULT = withPos(new ListExp(),lleft,rright); :} |
		LVECTOR:l exps:es RBRACKET:r
			{: RESULT = withPos(new VectorExp(es),lleft,rright); :} |
		LVECTOR:l RBRACKET:r
			{: RESULT = withPos(new VectorExp(),lleft,rright); :} |
		LET:l decs:ds IN exp:e END:en
			{: RESULT = withPos(new LetExp(ds,e),lleft,enright); :} |
		LET:l decs:ds IN expseq:es END:e
			{: RESULT = withPos(new LetExp(ds, withPos(new SeqExp(es),esleft,esright)),lleft,eright); :};

	label ::=
		ident1:id {: RESULT = withPos(new IdentLabel(id),idleft,idright); :} |
		INT:i {: RESULT = withPos(new IntLabel(i),ileft,iright); :};

	exprecord ::=
		expfield:f {: RESULT = list(f); :} |
		exprecord:r COMMA expfield:f {: RESULT = append(r,f); :};
				
	expfield ::= label:l EQUALS exp:e {: RESULT = withPos(new ExpField(l,e),lleft,eright); :};

	expseq ::=
		exp:e1 SEMIC exp:e2 {: RESULT = append(list(e1),e2); :} |
		expseq:s SEMIC exp:e {: RESULT = append(s,e); :};

	exps ::=
		exp:e {: RESULT = list(e); :} |
		exps:es COMMA exp:e {: RESULT = append(es,e); :};

	appexp ::=
		atexp:e {: RESULT = list(e); :} |
		appexp:ae atexp:e {: RESULT = append(ae,e); :};

	exp ::=
		exp:e HANDLE match:m
			{: RESULT = withPos(new HandleExp(e, m),eleft,mright); :} |
		exp:e1 ORELSE exp:e2
			{: RESULT = withPos(new OrelseExp(e1,e2),e1left,e2right); :} |
		exp:e1 ANDALSO exp:e2
			{: RESULT = withPos(new AndalsoExp(e1,e2),e1left,e2right); :} |
		exp:e COLON type:t
			{: RESULT = withPos(new TypedExp(e,t),eleft,tright); :} |
		appexp:ae
			{: RESULT = withPos(new FlatAppExp(ae),aeleft,aeright); :} |
		FN:f match:m
			{: RESULT = withPos(new FnExp(m),fleft,mright); :} |
		CASE:c exp:e OF match:m
			{: RESULT = withPos(new CaseExp(e,m),cleft,mright); :} |
		WHILE:w exp:e1 DO exp:e2
			{: RESULT = withPos(new WhileExp(e1,e2),wleft,e2right); :} |
		IF:i exp:e1 THEN exp:e2 ELSE exp:e3
			{: RESULT = withPos(new IfExp(e1,e2,e3),ileft,e3right); :} |
		RAISE:r exp:e
			{: RESULT = withPos(new RaiseExp(e),rleft,eright); :} |
		error:e
			{: RESULT = withPos(new ErrorExp(),eleft,eright); :};

	// Matches

	match ::= rules:rs {: RESULT = withPos(new Match(rs),rsleft,rsright); :};

	rules ::=
		pat:p DARROW exp:e
			{: RESULT = list(withPos(new Rule(p,e),pleft,eright)); :} |
		rules:r1 BAR rules:r2
			{: RESULT = concat(r1,r2); :};

	// Declarations

	decs ::=

			{: RESULT = list(); :} |
		LOCAL:l decs:ds1 IN decs:ds2 END:e
			{: RESULT = list(withPos(new LocalDec(ds1,ds2),lleft,eright)); :} |
		decs:ds dec:d
			{: RESULT = append(ds,d); :} |
		decs:ds error:e
			{: RESULT = append(ds, withPos(new ErrorDec(),eleft,eright)); :} |
		decs:ds SEMIC
			{: RESULT = ds; :};

	dec ::=
		VAL:v valbind:vb
			{: RESULT = withPos(new ValDec(vb),vleft,vbright); :} |
		VAL:v tyvarseq:tvs valbind:vb
			{: RESULT = withPos(new ValDec(vb,tvs),vleft,vbright); :} |
		VAL:v REC recvalbind:rvb
			{: RESULT = withPos(new RecValDec(rvb),vleft,rvbright); :} |
		VAL:v REC tyvarseq:tvs recvalbind:rvb
			{: RESULT = withPos(new RecValDec(rvb,tvs),vleft,rvbright); :} |
		FUN:f funbind:fb
			{: RESULT = withPos(new FunDec(fb),fleft,fbright); :} |
		TYPE:t typebind:tb
			{: RESULT = withPos(new TypeDec(tb),tleft,tbright); :} |
		DATATYPE:d datatypebind:db
			{: RESULT = withPos(new DatatypeDec(db),dleft,dbright); :} |
		DATATYPE:d datatypebind:db WITHTYPE typebind:tb
			{: RESULT = withPos(new DatatypeDec(db,tb),dleft,tbright); :} |
		ABSTYPE:a datatypebind:db WITH decs:ds END:e
			{: RESULT = withPos(new AbstypeDec(db,ds),aleft,eright); :} |
		ABSTYPE:a datatypebind:db WITHTYPE typebind:tb WITH decs:ds END:e
			{: RESULT = withPos(new AbstypeDec(db,tb,ds),aleft,eright); :} |
		EXCEPTION:e exnbind:eb
			{: RESULT = withPos(new ExnDec(eb),eleft,ebright); :} |
		OPEN:o qidents1:ids
			{: RESULT = withPos(new OpenDec(ids),oleft,idsright); :} |
		fixity:f idents2:ids
			{: RESULT = withPos(new FixityDec(f,ids),fleft,idsright); :};
			

	valbind ::=
		pat:p EQUALS exp:e
			{: RESULT = list(withPos(new ValBind(p,e),pleft,eright)); :} |
		LAZY:l pat:p EQUALS exp:e
			{: RESULT = list(withPos(new ValBind(p,e,true),lleft,eright)); :} |
		valbind:vb1 AND valbind:vb2
			{: RESULT = concat(vb1,vb2); :};
	
	recvalbind ::=
		op_:o ident1:id constraint:c EQUALS exp:e
			{: RESULT = list(withPos(new RecValBind(id,c,e),oleft,eright)); :} |
		LAZY:l op_ ident1:id constraint:c EQUALS exp:e
			{: RESULT = list(withPos(new RecValBind(id,c,e,true),lleft,eright)); :} |
		recvalbind:vb1 AND recvalbind:vb2
			{: RESULT = concat(vb1,vb2); :};
	
	funbind ::=
		clauses:cs
			{: RESULT = list(withPos(new FunBind(cs),csleft,csright)); :} |
		LAZY:l clauses:cs
			{: RESULT = list(withPos(new FunBind(cs,true),lleft,csright)); :} |
		funbind:fb1 AND funbind:fb2
			{: RESULT = concat(fb1,fb2); :};
	
	clauses ::=
		atpats:ps constraint:c EQUALS exp:e
			{: RESULT = list(withPos(new Clause(ps,c,e),psleft,eright)); :} |
		clauses:c1 BAR clauses:c2
			{: RESULT = concat(c1,c2); :};
	
	typebind ::=
		tyvars:tvs ident:id EQUALS type:t
			{: RESULT = list(withPos(new TypeBind(tvs,id,t),tvsleft,tright)); :} |
		typebind:tb1 AND typebind:tb2
			{: RESULT = concat(tb1,tb2); :};
	
	datatypebind ::=
		tyvars:tvs ident:id EQUALS conbind:cbs
			{: RESULT = list(withPos(new DatatypeBind(tvs,id,cbs),tvsleft,cbsright)); :} |
		LAZY:l tyvars:tvs ident:id EQUALS conbind:cbs
			{: RESULT = list(withPos(new DatatypeBind(tvs,id,cbs,true),lleft,cbsright)); :} |
		tyvars:tvs ident:id EQUALS DATATYPE tycon:tc
			{: RESULT = list(withPos(new DatatypeBind(tvs,id,tc),tvsleft,tcright)); :} |
		LAZY:l tyvars:tvs ident:id EQUALS DATATYPE tycon:tc
			{: RESULT = list(withPos(new DatatypeBind(tvs,id,tc,true),lleft,tcright)); :} |
		datatypebind:db1 AND datatypebind:db2
			{: RESULT = concat(db1,db2); :};
	
	conbind ::=
		op_:o ident2:id
			{: RESULT = list(withPos(new ConBind(id),oleft,idright)); :} |
		op_:o ident2:id OF type:t
			{: RESULT = list(withPos(new ConBind(id,t),oleft,tright)); :} |
		conbind:cb1 BAR conbind:cb2
			{: RESULT = concat(cb1,cb2); :};
	
	exnbind ::=
		op_:o ident2:id
			{: RESULT = list(withPos(new ExnBind(id),oleft,idright));
				id.setPos(idleft,idright); :} |
		op_:o ident2:id OF type:t
			{: RESULT = list(withPos(new ExnBind(id,t),oleft,tright)); 
				id.setPos(idleft, idright); t.setPos(tleft,tright); :} |
		op_:o ident2:id1 EQUALS qident2:id2
			{: RESULT = list(withPos(new ExnBind(id1,id2),oleft,id2right));
				id1.setPos(id1left,id1right); id2.setPos(id2left,id2right); :} |
		exnbind:eb1 AND exnbind:eb2
			{: RESULT = concat(eb1,eb2); :};

	fixity ::=
		INFIX:f {: RESULT = withPos(new Fixity(Fixity.LEFT),fleft,fright); :} |
		INFIX:f INT:i {: RESULT = withPos(new Fixity(Fixity.LEFT, i),fleft,iright); :} |
		INFIXR:f {: RESULT = withPos(new Fixity(Fixity.RIGHT),fleft,fright); :} |
		INFIXR:f INT:i {: RESULT = withPos(new Fixity(Fixity.RIGHT, i),fleft,iright); :} |
		NONFIX:f {: RESULT = withPos(new Fixity(Fixity.NONE),fleft,fright); :};
			
	// Patterns

	atpat ::=
		ident1:id
			{: RESULT = withPos(new VarPat(id),idleft,idright); :} |
		OP:o ident2:id
			{: RESULT = withPos(new VarPat(id, true),oleft,idright); :} |
		longident2:id
			{: RESULT = withPos(new VarPat(id),idleft,idright); :} |
		INT:i
			{: RESULT = withPos(new IntPat(i),ileft,iright); :} |
		STRING:s
			{: RESULT = withPos(new StringPat(s),sleft,sright); :} |
		CHAR:c
			{: RESULT = withPos(new CharPat(c),cleft,cright); :} |
		UNDER:u
			{: RESULT = withPos(new WildPat(),uleft,uright); :} |
		LBRACKET:l RBRACKET:r
			{: RESULT = withPos(new ListPat(),lleft,rright); :} |
		LBRACKET:l pats:ps RBRACKET:r
			{: RESULT = withPos(new ListPat(ps),lleft,rright); :} |
		LVECTOR:l RBRACKET:r
			{: RESULT = withPos(new VectorPat(),lleft,rright); :} |
		LVECTOR:l pats:ps RBRACKET:r
			{: RESULT = withPos(new ListPat(ps),lleft,rright); :} |
		LBRACE:l RBRACE:r
			{: RESULT = withPos(new RecordPat(),lleft,rright); :} |
		LBRACE:l patrecord:r RBRACE:rb
			{: RESULT = withPos(new RecordPat(r),lleft,rbright); :} |
		LPAREN:l RPAREN:r
			{: RESULT = withPos(new UnitPat(),lleft,rright); :} |
		LPAREN:l pat:p COMMA pats:ps RPAREN:r
			{: RESULT = withPos(new TuplePat(prepend(p,ps)),lleft,rright); :} |
		LPAREN:l orpat:op RPAREN:r
			{: RESULT = withPos(new OrPat(op),lleft,rright); :} |
		LPAREN:l pat:p RPAREN:r
			{: RESULT = withPos(p,lleft,rright); :};

	patrecord ::=
		patfield:f {: RESULT = list(f); :} |
		patfield:f COMMA patrecord:r {: RESULT = prepend(f,r); :} |
		DOTDOTDOT {: RESULT = list(); :};
	
	patfield ::=
		label:l EQUALS pat:p
			{: RESULT = withPos(new PatField(l,p),lleft,pright); :} |
		ident:id constraint:c
			{: RESULT = withPos(new VarPatField(id,c),idleft,cright); :} |
		ident:id constraint:c AS pat:p
			{: RESULT = withPos(new VarPatField(id,c,p),idleft,pright); :};
	
	orpat ::=
		pat:p1 BAR pat:p2 {: RESULT = append(list(p1),p2); :} |
		orpat:op BAR pat:p {: RESULT = append(op,p); :};
	
	pats ::=
		pat:p {: RESULT = list(p); :} |
		pats:ps COMMA pat:p {: RESULT = append(ps,p); :};

	atpats ::=
		atpat:p {: RESULT = list(p); :} |
		atpats:ps atpat:p {: RESULT = append(ps,p); :};

	pat ::=
		atpats:ps
			{: RESULT = withPos(new FlatConPat(ps),psleft,psright); :} |
		pat:p1 AS pat:p2
			{: RESULT = withPos(new LayeredPat(p1,p2),p1left,p2right); :} |
		pat:p COLON type:t
			{: RESULT = withPos(new TypedPat(p,t),pleft,tright); :} |
		error:e
			{: RESULT = withPos(new ErrorPat(),eleft,eright); :};

	// Types
	
	type ::=
		typetuple:tt
			{: RESULT = withPos(new TupleType(tt),ttleft,ttright); :} |
		type:t1 ARROW type:t2
			{: RESULT = withPos(new FnType(t1,t2),t1left,t2right); :} |
		attype:t
			{: RESULT = t; :} |
		error:e
			{: RESULT = withPos(new ErrorType(),eleft,eright); :};
	
	attype ::=
		tyvar:tv
			{: RESULT = withPos(new VarType(tv),tvleft,tvright); :} |
		LBRACE:l typerecord:tr RBRACE:r
			{: RESULT = withPos(new RecordType(tr),lleft,rright); :} |
		LBRACE:l RBRACE:r
			{: RESULT = withPos(new RecordType(),lleft,rright); :} |
		LPAREN:l type:t COMMA types:ts RPAREN tycon:tc
			{: RESULT = withPos(new ConType(prepend(t,ts),tc),lleft,tcright); :} |
		attype:t tycon:tc
			{: RESULT = withPos(new ConType(t,tc),tleft,tcright); :} |
		tycon:tc
			{: RESULT = withPos(new ConType(tc),tcleft,tcright); :} |
		LPAREN:l type:t RPAREN:r
			{: RESULT = withPos(t,lleft,rright); :};
	
	tycon ::=
		qident:id {: RESULT = id; :};
	
	typetuple ::=
		attype:t1 ASTERISK attype:t2 {: RESULT = append(list(t1),t2); :} |
		typetuple:tt ASTERISK attype:t {: RESULT = append(tt,t); :};

	typerecord ::=
		typefield:f {: RESULT = list(f); :} |
		typerecord:r COMMA typefield:f {: RESULT = append(r,f); :};
				
	typefield ::= label:l COLON type:t {: RESULT = withPos(new TypeField(l,t),lleft,tright); :};
	
	types ::=
		type:t {: RESULT = list(t); :} |
		types:ts COMMA type:t {: RESULT = append(ts,t); :};

	// Modules
	
	str ::=
		STRUCT:s strdecs:sds END:e
			{: RESULT = withPos(new BaseStr(sds),sleft,eright); :} |
		qident2:id
			{: RESULT = withPos(new VarStr(id),idleft,idright); :} |
		qident2:id fctargs:fas
			{: RESULT = withPos(new AppStr(id,fas),idleft,fasright); :} |
		LET:l strdecs:sds IN str:s END:e
			{: RESULT = withPos(new LetStr(sds,s),lleft,eright); :} |
		str:s COLON sig:sig
			{: RESULT = withPos(new SignedStr(s,sig),sleft,sigright); :} |
		str:s COLONGT sig:sig
			{: RESULT = withPos(new SignedStr(s,sig,true),sleft,sigright); :} |
		error
			{: RESULT = new ErrorStr(); :};
	
	fctarg ::=
		LPAREN str:s RPAREN {: RESULT = s; :} |
		LPAREN:l strdecs_:sds RPAREN:r {: RESULT = withPos(new BaseStr(sds),lleft,rright); :};
	
	fctargs ::=
		fctarg:fa {: RESULT = list(fa); :} |
		fctargs:fas fctarg:fa {: RESULT = append(fas,fa); :};
	
	strdecs ::=
		
			{: RESULT = list(); :} |
		strdecs:sds strdec:sd
			{: RESULT = append(sds,sd); :} |
		strdecs:sds SEMIC
			{: RESULT = sds; :};
	
	strdecs_ ::=
		
			{: RESULT = list(); :} |
		strdecs_:sds strdec_:sd
			{: RESULT = append(sds,sd); :} |
		strdecs_:sds SEMIC
			{: RESULT = sds; :};
	
	strdec_ ::=
		dec:d
			{: RESULT = d; :} |
		STRUCTURE:s strbind:sb
			{: RESULT = withPos(new StrDec(sb),sleft,sbright); :} |
		FUNCTOR:f fctbind:fb
			{: RESULT = withPos(new FctDec(fb),fleft,fbright); :} |
		LOCAL:l strdecs:sd1 IN strdecs:sd2 END:e
			{: RESULT = withPos(new LocalDec(sd1,sd2),lleft,eright); :};
	
	strdec ::=
		strdec_:sd
			{: RESULT = sd; :} |
		error
			{: RESULT = new ErrorDec(); :};
		
	strbind ::=
		ident2:id EQUALS str:s
			{: RESULT = list(withPos(new StrBind(id,s),idleft,sright)); :} |
		ident2:id COLON sig:sig EQUALS str:s
			{: RESULT = list(withPos(new StrBind(id,s,sig),idleft,sright)); :} |
		ident2:id COLONGT sig:sig EQUALS str:s
			{: RESULT = list(withPos(new StrBind(id,s,sig,true),idleft,sright)); :} |
		strbind:sb1 AND strbind:sb2
			{: RESULT = concat(sb1,sb2); :};
	
	fctbind ::=
		ident2:id paramdescs:pds EQUALS str:s
			{: RESULT = list(withPos(new FctBind(id,pds,s),idleft,sright)); :} |
		ident2:id paramdescs:pds COLON sig:sig EQUALS str:s
			{: RESULT = list(withPos(new FctBind(id,pds,sig,s),idleft,sright)); :} |
		ident2:id paramdescs:pds COLONGT sig:sig EQUALS str:s
			{: RESULT = list(withPos(new FctBind(id,pds,sig,s,true),idleft,sright)); :} |
		fctbind:fb1 AND fctbind:fb2
			{: RESULT = concat(fb1,fb2); :};
				
	sig ::=
		SIG:s specs:ss END:e
			{: RESULT = withPos(new BaseSig(ss),sleft,eright); :} |
		ident2:id
			{: RESULT = withPos(new VarSig(id),idleft,idright); :} |
		sig:s WHERE wherespecs:ws
			{: RESULT = withPos(new WhereSig(s,ws),sleft,wsright); :} |
		error
			{: RESULT = new ErrorSig(); :};
	
	wherespecs ::=
		TYPE:ty tyvars:tvs qident2:id EQUALS type:t
			{: RESULT = list(withPos(new TypeWhspec(tvs,id,t),tyleft,tright)); :} |
		qident2:id1 EQUALS qident2:id2
			{: RESULT = list(withPos(new StructWhspec(id1,id2),id1left,id2right)); :} |
		wherespecs:ws1 AND wherespecs:ws2
			{: RESULT = concat(ws1,ws2); :};

	sigbind ::=
		ident2:id EQUALS sig:s
			{: RESULT = list(withPos(new SigBind(id,s),idleft,sright)); :} |
		sigbind:sb1 AND sigbind:sb2
			{: RESULT = concat(sb1,sb2); :};
	
	funsigbind ::=
		ident2:id paramdescs:pds EQUALS sig:s
			{: RESULT = list(withPos(new FunsigBind(id,pds,s),idleft,sright)); :} |
		funsigbind:fsb1 AND funsigbind:fsb2
			{: RESULT = concat(fsb1,fsb2); :};
		
	specs ::=
			{: RESULT = list(); :} |
		spec:s specs:ss {: RESULT = prepend(s,ss); :} |
		SEMIC specs:ss {: RESULT = ss; :};

	spec ::=
		STRUCTURE:s strdesc:sd
			{: RESULT = withPos(new StrSpec(sd),sleft,sdright); :} |
		FUNCTOR:f fctdesc:fd
			{: RESULT = withPos(new FctSpec(fd),fleft,fdright); :} |
		DATATYPE:d datatypebind:db
			{: RESULT = withPos(new DatatypeSpec(db),dleft,dbright); :} |
		DATATYPE:d datatypebind:db WITHTYPE typebind:tb
			{: RESULT = withPos(new DatatypeSpec(db,tb),dleft,tbright); :} |
		TYPE:t typedesc:td
			{: RESULT = withPos(new TypeSpec(td),tleft,tdright); :} |
		EQTYPE:eq typedesc:td
			{: RESULT = withPos(new TypeSpec(td,true),eqleft,tdright); :} |
		VAL:v valdesc:vd
			{: RESULT = withPos(new ValSpec(vd),vleft,vdright); :} |
		EXCEPTION:e exndesc:ed
			{: RESULT = withPos(new ExnSpec(ed),eleft,edright); :} |
		fixity:f idents2:ids
			{: RESULT = withPos(new FixitySpec(f,ids),fleft,idsright); :} |
		SHARING:s sharingdesc:sd
			{: RESULT = withPos(new SharingSpec(sd),sleft,sdright); :} |
		INCLUDE:i sig:s
			{: RESULT = withPos(new IncludeSpec(s),ileft,sright); :} |
		INCLUDE:i ident2:id idents2:ids
			{: RESULT = withPos(new IncludeSpec(prepend(id,ids)),ileft,idsright); :};
	
	strdesc ::=
		ident2:id COLON sig:s
			{: RESULT = list(withPos(new StrDesc(id,s),idleft,sright)); :} |
		ident2:id1 COLON sig:s EQUALS qident2:id2
			{: RESULT = list(withPos(new StrDesc(id1,s,id2),id1left,id2right)); :} |
		strdesc:sd1 AND strdesc:sd2
			{: RESULT = concat(sd1,sd2); :};
	
	fctdesc ::=
		ident2:id1 paramdescs:pds COLON ident2:id2
			{: RESULT = list(withPos(new FctDesc(id1,pds,id2),id1left,id2right)); :} |
		fctdesc:fd1 AND fctdesc:fd2
			{: RESULT = concat(fd1,fd2); :};
	
	typedesc ::=
		tyvars:tvs ident:id
			{: RESULT = list(withPos(new TypeDesc(tvs,id),tvsleft,idright)); :} |
		tyvars:tvs ident:id EQUALS type:t
			{: RESULT = list(withPos(new TypeDesc(tvs,id,t),tvsleft,tright)); :} |
		typedesc:td1 AND typedesc:td2
			{: RESULT = concat(td1,td2); :};
	
	valdesc ::=
		op_ ident:id COLON type:t
			{: RESULT = list(withPos(new ValDesc(id,t),idleft,tright)); :} |
		valdesc:vd1 AND valdesc:vd2
			{: RESULT = concat(vd1,vd2); :};

	exndesc ::=
		ident:id
			{: RESULT = list(withPos(new ExnDesc(id),idleft,idright)); :} |
		ident:id OF type:t
			{: RESULT = list(withPos(new ExnDesc(id,t),idleft,tright)); :} |
		exndesc:ed1 AND exndesc:ed2
			{: RESULT = concat(ed1,ed2); :};
	
	sharingdesc ::=
		patheqn:pe {: RESULT = list(withPos(new SharingDesc(pe,true),peleft,peright)); :} |
		TYPE:t patheqn:pe {: RESULT = list(withPos(new SharingDesc(pe,false),tleft,peright)); :} |
		sharingdesc:sd1 AND sharingdesc:sd2 {: RESULT = concat(sd1,sd2); :};

	patheqn ::=
		qident2:id1 EQUALS qident2:id2 {: RESULT = append(list(id1),id2); :} |
		patheqn:pe EQUALS qident2:id {: RESULT = append(pe,id); :};
	
	paramdesc ::=
		ident:id COLON sig:s
			{: RESULT = withPos(new ParamDesc(id,s),idleft,sright); :} |
		specs:ss
			{: RESULT = new ParamDesc(withPos(new BaseSig(ss),ssleft,ssright)); :};
	
	paramdescs ::=
			{: RESULT = list(); :} |
		paramdescs:pds LPAREN paramdesc:pd RPAREN {: RESULT = append(pds,pd); :};
	
	// Top-level declarations
	
	topdecs ::=
			{: RESULT = list(); :} |
		exp:e
			{: RESULT = list(withPos(new ExpDec(e),eleft,eright)); :} |
		topdecs:tds topdec:td
			{: RESULT = append(tds,td); :} |
		topdecs:tds SEMIC
			{: RESULT = tds; :};
	
	topdec ::=
		strdec_:sd
			{: RESULT = sd; :} |
		SIGNATURE:s sigbind:sb
			{: RESULT = withPos(new SigDec(sb),sleft,sbright); :} |
		FUNSIG:f funsigbind:fsb
			{: RESULT = withPos(new FunsigDec(fsb),fleft,fsbright); :} |
		SEMIC:s exp:e
			{: RESULT = withPos(new ExpDec(e),sleft,eright); :};
